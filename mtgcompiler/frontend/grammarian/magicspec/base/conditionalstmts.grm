//Conditional statements

conditionalstatement: ifstatement
| wheneverstatement
| whenstatement
| atstatement
| aslongasstatement
| forstatement
| untilstatement
| afterstatement
| otherwisestatement
| unlessstatement
| asstatement
| whilestatement
| duringstatement
| exceptstatement
| ratherstatement
| nexttimestatement
| beforestatement
| thencontinuationstatement

ifstatement: "if" statement "," statement
| statement "only"? "if" statement -> ifstatementinv

wheneverstatement:  "whenever" statement timeexpression? "," statement 
| statement "whenever" statement timeexpression? -> wheneverstatementinv

whenstatement:  "when" statement "," statement 
| statement "when" statement -> whenstatementinv

atstatement:  "at" timeexpression "," statement 
| statement "at" timeexpression -> atstatementinv

aslongasstatement:  "for"? "as" "long" "as" statement "," statement
| statement "for"? "as" "long" "as" statement -> aslongasstatementinv

forstatement:  "for" (entity | "each" "time" statement | statement) "," statement
| statement "for" (entity | "each" "time" statement | statement) -> forstatementinv

//"for" "each"
//forstatement:  "for" "each" (entity | "time" statement) ("beyond" "the" "first")? "," statement 
//| statement "for" "each" (entity | "time" statement) ("beyond" "the" "first")? -> forstatementinv

untilstatement:  "until" timeexpression "," statement 
| statement "until" timeexpression -> untilstatementinv

afterstatement:  "after" timeexpression "," statement 
| statement "after" timeexpression -> afterstatementinv

otherwisestatement: "otherwise" "," statement

unlessstatement:  statement "unless" statement

asstatement: "as" statement "," statement -> asstatement

whilestatement:  "while" statement "," statement

duringstatement:  statement "during" timeexpression
| statement "only" "during" timeexpression -> exclusiveduringstatement

exceptstatement:  "except" (("by"|"for") entity | statement) "," statement
| statement "except" (("by"|"for") entity | statement) -> exceptstatementinv

ratherstatement:  statement "rather" "than" statement

nexttimestatement: "the" "next" "time" statement timeexpression? "," statement

beforestatement: statement "before" (timeexpression | statement)

thencontinuationstatement: statement "," "then" statement
| "then" statement 
