//A regular declaration introduces something that the card relates to or acts on.
//Example(s): "destroy *target nonblack creature*" and "draw *a card*"
//A reference points to a declaration that was made earlier or that is already assumed to exist.
//Example(s) : "draw a card if *that creature* had power 3 or greater" or "target creature blocks *this turn* if able"
declaration.5: singledeclaration | compounddeclaration
compounddeclaration.5:  singledeclaration ("," singledeclaration ",")* "or" singledeclaration -> orcompounddeclaration
| singledeclaration ("," singledeclaration ",")* "and" singledeclaration -> andcompounddeclaration
| singledeclaration ("," singledeclaration ",")* "and/or" singledeclaration -> andorcompounddeclaration
| "neither" singledeclaration "nor" singledeclaration -> neithernorcompounddeclaration
singledeclaration.5: regulardeclaration | referencedeclaration | keywordabilitydeclaration
regulardeclaration.5: declarationdecorator* declarationdefinition
referencedeclaration.5: referencedecorator+ declarationdefinition | personalpronounreferencedefinition | namereferencedefinition

//Reference definition for third-person personal pronoun references.
//Example(s): "*they* can’t be regenerated." and "Sarkhan the Mad deals damage to *himself*"
personalpronounreferencedefinition.5: PERSONALPRONOUNREFERENCE | SELFPRONOUNREFERENCE | HERAMBIGUOUSREFERENCE
HERAMBIGUOUSREFERENCE.100: "her" //This could be the possessive form or the object of a sentence.
PERSONALPRONOUNREFERENCE.90: ("he" | "him") | ("she") | ("they" | "them") | "it" | "you"
SELFPRONOUNREFERENCE.110:  "itself" | "himself" | "herself" | "themselves"

//Reference form used when when a card makes a reference to itself by name. "~" is a full name reference, "~f" (rarely used) is a first name reference.
//Example(s): "*Chandra, Fire of Kaladesh (~)* deals 1 damage to target player or planeswalker. If *Chandra (~f)* has dealt 3 or more damage [...]"
namereferencedefinition.5: NAMEREFSYMBOL
NAMEREFSYMBOL.5: "~" | "~f"

//Reference decorators help clarify what is being talked about. Note that reference decorators can have nested
//references to other defined things (like "your *opponent's* hand").
//Example(s): "Destroy target creature. *Its* controller [...]" and "*its owner’s* hand." "*that creature’s* toughness."
referencedecorator.5: THIRDPERSONIMPERSONALREFERENCE | POSSESSIVEPRONOUNREFERNCE | HERAMBIGUOUSREFERENCE
THIRDPERSONIMPERSONALREFERENCE.5: ("that" | "those") | ("this"|"these")
POSSESSIVEPRONOUNREFERNCE.150: "its" | "your" | "their" | "his" //Note.5: "her" is handled separately to capture the ambiguity.

//Declaration decorators. The most popular decorator is "target".
//Example(s): "destroy *target* nonblack creature", "*any* permanent"
declarationdecorator.5: "each" -> eachdecorator
| "same" -> samedecorator
| "all" -> alldecorator
| "other" -> otherdecorator
| "another" -> anotherdecorator
| ("a" | "an") -> indefinitearticledecorator
| "the" -> definitearticledecorator
| valueterm? "target" -> targetdecorator
| "any" -> anydecorator

//A declaration definition is made up of a set of descriptive terms like "a *blue permanent*" or "*two 1/1 green elf creature tokens*".
//There can be limited nesting of declarations inside definitions like with possessives ("its *owner's* hand") and prepositions
//("a creature *with a +1/+1 counter* *on it*").
//declarationdefinition.5: "DEFINITION" | (nonnesteddeclarationterm | nesteddeclarationterm)+ //TODO.5: Remove "DEFINITION" placeholder
declarationdefinition.5: declarationdefinition_standardorder //| declarationdefinition_nonstandardorder
nonnesteddeclarationterm : (valueterm | manaterm | characteristicterm
| typeterm | colorterm | modifierterm | qualifierterm | timeterm | damageterm | playerterm
| powertoughnessterm | loyaltycostterm | counterterm | nameterm | variableterm | zoneterm | anytargetterm  | tapuntapsymbol | specialterm)
nesteddeclarationterm.5: possessiveclarationreference | prepositionalattacheddeclaration

//a 1/1 white Human creature token
//a 1/1 white Spirit creature token with flying
//a 1/1 red Mercenary creature token with "{T}: Target creature you control gets +1/+0 until end of turn. Activate only as a sorcery."
//permanents you control
//up to one target non-Aura enchantment card
//a Knight, Aura, Equipment, or legendary artifact card
//a 1/1 black and green Insect creature token
//its controller’s graveyard, hand, and library
// target nonwhite, nonblack creature.
//a blue, black, or red spell
//Noninstant, nonsorcery cards
//each foretold card you own in exile
declarationdefinition_standardorder.100: possessiveclarationreference* valueterm? powertoughnessterm? modifierterm* colorterm* (damageterm | playerterm | typeterm | loyaltycostterm | tapuntapsymbol | zoneterm | timeterm | manaterm | characteristicterm | qualifierterm)+ prepositionalattacheddeclaration*
| anytargetterm
| valueterm? counterterm
| nameterm
| specialterm
//declarationdefinition_nonstandardorder.0: (nonnesteddeclarationterm | nesteddeclarationterm)+ //This one should only be used if we're misinterpreting the declaration, like saying "create *a 1/1 black* and *green insect creature token with flying*." has two declarations in it.





//Declaration definitions can have prepositional phrases and possessives that can contain declarations of their own.
//We need a special rule for embedded declarations which shouldn't have any further recursive declarations. Instead,
//we treat the declaration as flat and it's the job of a semantic analyzer to determine how they are really nested.
//For example, we parse "the owner of the creature with a +1/+1 counter on it" as
//"the owner (of the creature) (with a +1/+1 counter) (on it)" rather than
//"the owner (of the creature (with a +1/+1 counter (on it)))", if that makes sense.
embeddedsingledeclaration.5: embeddedregulardeclaration | embeddedreferencedeclaration
embeddedregulardeclaration.5: declarationdecorator* embeddeddeclarationdefinition
embeddedreferencedeclaration.5: referencedecorator+ embeddeddeclarationdefinition | personalpronounreferencedefinition | namereferencedefinition
embeddeddeclarationdefinition.5: declarationdefinition_standardorder
prepositionalattacheddeclaration.5: DECLARATIONPREPOSITION embeddedsingledeclaration
DECLARATIONPREPOSITION.5: PREPOSITION_OF | PREPOSITION_ON | PREPOSITION_WHO | PREPOSITION_IN | PREPOSITION_UNDER | PREPOSITION_WITHOUT | PREPOSITION_WITH | "as" "though"? //TODO: Do we handle "as (though)" at the statement level?

PREPOSITION_OF.5: "of"
PREPOSITION_ON.5: "on"
PREPOSITION_WHO.5: "who"
PREPOSITION_IN.5: "in"
PREPOSITION_UNDER.5: "under"
PREPOSITION_WITHOUT.10: "without"
PREPOSITION_WITH.5: "with"


possessiveclarationreference.5: (embeddeddeclarationdefinition | namereferencedefinition) ("'s"|"'")

specialterm.5: MECHANICSPECIFICTERM
MECHANICSPECIFICTERM.5: "ring" //Example: "the ring has tempted you two or more times this game"
| "dungeon" //Example: "venture into the dungeon"
| "city's blessing" //Example: "As long as you have the city’s blessing"
| "evidence" //Example: "If you collected evidence"

negationofterm.5: NONPREFIX
NONPREFIX.5: "non" ("-")?
typeterm.5: negationofterm? (TYPE ["s"] | SUBTYPESPELL ["s"] | SUBTYPELAND ["s"] | SUBTYPEARTIFACT ["s"] | SUBTYPEENCHANTMENT ["s"] | SUBTYPEPLANESWALKER | SUBTYPECREATUREA ["s"] | SUBTYPECREATUREB ["s"] | SUBTYPEPLANAR | SUPERTYPE)
modifierterm.5: negationofterm? (ABILITYMODIFIER | KEYWORDSTATUSMODIFIER  | RELATIVEMODIFIER | effectstatusmodifier | playerstatusmodifier)
qualifierterm.5: negationofterm? QUALIFIER["s"]
timeterm.5: PHASE | STEP | TURN | GAME
playerterm.5: PLAYER["s"]
zoneterm.5: ZONE
powertoughnessterm.5: valueterm "/" valueterm
loyaltycostterm.5: (PLUS | MINUS) valueterm
counterterm.5: (countertype | powertoughnessterm)  "counter"["s"]
characteristicterm.5: negationofterm? (OBJECTCHARACTERISTIC | PLAYERCHARACTERISTIC)
nameterm.5: "OBJECTNAME" | ARBITRARYWORD
damageterm.5: negationofterm? DAMAGETYPE
colorterm.5: negationofterm? COLOR

variableterm.5: "choice"["s"] //Example(s): "their choice", "of your choice"
| ("copy" | "copies") //Example(s): "You may have Shapeshifters you control become *copies* of that creature", "choose new targets for the *copy*"
| "vote"["s"] //Example(s): "each *vote* they receive"
| NUMBERPROPERTY? "number"["s"] //Example(s): "the *number* of cards you drew this term", "a *prime number* of lands"
| "time"["s"] //Example(s): "the number of *times* ~ was kicked", "the first time this turn"
| "pile" //Example: "target opponent looks at the top three cards of your library and separates them into a face-down pile and a face-up pile."
| "result" //Example: "draw cards equal to the *result*"
| "rest" //Example: "You have no maximum hand size for the *rest* of the game."
| "order" //Example: "Put that card onto the battlefield and the rest on the bottom of your library in a random *order*."
| "way" //Example: "Creatures destroyed this *way* can't be regenerated."
| "beginning" //Example: "the *beginning* of your next upkeep"
| "end" //Example: "until *end* of turn"
anytargetterm.5: "any target" //Special nullary variant like "~ deals three damage to *any target*"


//Keyworded ability declaration
//Regular keyworded abilities are those that are of the form "keyword (cost/description"?.
//Note that the parser will allow for illegal keyworded ability definitions like "flying 3", and it's the
//job of the semantic analyzer to check for things like this. Special keyworded abilities, meanwhile, are
//those that have very particular syntax around them and its easier to spell them out in a special rule.
keywordabilitydeclaration.5: regularkeywordedabilitydefinition | specialkeywordedabilitydefinition

regularkeywordedabilitydefinition.5: REGULARKEYWORDEDABILITY declaration? //Example(s): "Flying", "Bushido 3", "Champion a Kithkin"
| REGULARKEYWORDEDABILITY declaration DASH declaration //Example(s): "Impending 4—{2}{R}{R}"

specialkeywordedabilitydefinition.5: "specialkeywordedabilitydefinition" //TODO

//need to handle past tense of keyworded actions like "kicked" or "plotted" as part of declarations
//special cases include...
//landwalk, hexproof from X and from Y, [typeexpression] "cycling" cost, "splice" "onto" typeexpression cost, typeexpression "offering"
//"forecast" activationstatement, "suspend" valuenumber cost, "aura swap" (if we wanted to generalize to TYPE swap)
//partner with objectname, kwcompanion.5: "companion", "craft" "with" declarationorreference cost, spree (Choose one or more additional costs.)

REGULARKEYWORDEDABILITY.5: "deathtouch" | "defender" | "double strike" | "enchant" | "equip" | "first strike" | "flash" | "flying"
| "haste" | "indestructible" | "intimidate" | "lifelink" | "reach" | "shroud" | "trample" | "vigilance" | "ward" | "rampage"
| "cumulative upkeep" | "flanking" | "phasing" | "buyback" | "shadow" | "echo" | "horsemanship" | "fading" | "multi"? "kicker"
| "flashback" | "madness" | "fear" | "mega"? "morph" | "amplify" | "provoke" | "storm" | "affinity" "for"? | "entwine" | "modular"
| "sunburst" | "bushido" | "soulshift" | "ninjutsu" | "epic" | "convoke" | "dredge" | "transmute" | "bloodthirst" | "haunt"
| "graft" | "recover" | "ripple" | "split second" | "suspend" | "vanishing" | "absorb" | "aura swap" | "delve" | "delve"
| "fortify" | "frenzy" | "gravestorm" | "poisonous" | "transfigure" | "champion" | "changeling" | "evoke" | "hideaway" | "prowl"
| "reinforce" | "conspire" | "persist" | "wither" | "retrace" | "devour" | "exalted" | "unearth" | "cascade" | "annihilator"
| "level up" | "rebound" | "totem armor" | "infect" | "battle cry" | "living weapon" | "undying" | "miracle" | "soulbond"
| "overload" | "scavenge" | "unleash" | "cipher" | "evolve" | "extort" | "fuse" | "bestow" | "tribute" | "dethrone"
| "hidden agenda" | "outlast" | "prowess" | "dash" | "exploit" | "menace" | "renown" | "awaken" | "devoid" | "ingest"
| "myriad" | "surge" | "skulk" | "emerge" | "escalate" | "melee" | "crew" | "fabricate" | "undaunted" | "improvise" | "aftermath"
| "embalm" | "eternalize" | "afflict" | "ascend" | "assist" | "jump-start" | "mentor" | "afterlife" | "riot" | "spectacle"
| "escape" | "mutate" | "oncore" | "boast" | "foretell" | "demonstrate" | ("day" | "night") "bound" | "disturb" | "decayed"
| "cleave" | "training" | "compleated" | "reconfigure" | "blitz" | "casualty" | "enlist" | "read ahead" | "ravenous" | "squad"
| "prototype" | "living metal" | "for mirrodin!" | "toxic" | "backup" | "bargain" | "disguise" | "plot" | "saddle"
| "gift" | "offspring" | "impending" | "time travel"


//Below are hardcoded game terms used in declarations.
PLAYER.100: "player" | "opponent" |  "teammate" | "team" | "controller" | "owner"
PLAYERCHARACTERISTIC.100: "maximum hand size" | "life total"["s"] | "life" | "cards in hand"
OBJECTCHARACTERISTIC.100: "card"? "name"["s"] | "mana value"["s"] | "color"["s"] | "color indicator"["s"] | "type"["s"] | "card type"["s"] | "subtype"["s"] | "supertype"["s"]
| "rules text" | ("ability" | "abilities" ) | "power" | "toughness" | "base power" | "base toughness" | "loyalty" | "hand modifier" | "life modifier"
ZONE.5: "battlefield" | "graveyard"["s"] | ("library"|"libraries") | "hand"["s"] | "stack" | "exile" | "command zone" | "outside the game" | "anywhere"
TYPE.100: "planeswalker" | "conspiracy" | "creature" | "enchantment" | "instant" | "land" | "phenomenon" | "plane" | "artifact" | "scheme" | "sorcery" | "tribal" | "vanguard"
SUBTYPESPELL.100: "arcane" | "trap" | "adventure"
SUBTYPELAND.100: "desert" | "forest" | "gate" | "island" | "lair" | "locus"
| "mine" | "mountain" | "plains" | "power-plant" | "swamp" | "tower" | "urza's"
SUBTYPEARTIFACT.100: "clue" | "contraption" | "equipment" | "fortification" | "treasure" | "vehicle" | "food"
SUBTYPEENCHANTMENT.100: "aura" | "cartouche" | "curse" | "saga" | "shrine" | "rune" | "shard" | "case" | "room"
SUBTYPEPLANESWALKER.100: "ajani" | "aminatou" | "angrath" | "arlinn" | "ashiok" | "bolas" | "chandra"
| "dack" | "daretti" | "domri" | "dovin" | "elspeth" | "estrid" | "freyalise" | "garruk" | "gideon"
| "huatli" | "jace" | "jaya" | "karn" | "kaya" | "kiora" | "koth" | "liliana" | "nahiri" | "narset"
| "nissa" | "nixilis" | "ral" | "rowan" | "saheeli" | "samut" | "sarkhan" | "sorin" | "tamiyo" | "teferi"
| "tezzeret" | "tibalt" | "ugin" | "venser" | "vivien" | "vraska" | "will" | "windgrace" | "xenagos"
| "yanggu" | "yanling"
//[TODO.5: SUBTYPECREATUREA and SUBTYPECREATUREB are split up because having such a long list of alternatives apparently]
//[causes Lark to suffer a recursion depth error. We should see if this is fixable.]
SUBTYPECREATUREA.100: "advisor" | "aetherborn" | ("ally"|"allies") | "angel" | "antelope" | "ape" | "archer" | "archon"
| "artificer" | "assassin" | "assembly-worker" | "atog" | "aurochs" | "avatar" | "azra" | "badger"
| "barbarian" | "basilisk" | "bat" | "bear" | "beast" | "beeble" | "berserker" | "bird" | "blinkmoth"
| "boar" | "bringer" | "brushwagg" | "camarid" | "camel" | "caribou" | "carrier" | "cat" | "centaur"
| "cephalid" | "chimera" | "citizen" | "cleric" | "cockatrice" | "construct" | "coward" | "crab"
| "crocodile" | "cyclops" | "dauthi" | "demon" | "deserter" | "detective" | "devil" | "dinosaur" | "djinn" | "dragon"
| "drake" | "dreadnought" | "drone" | "druid" | "dryad" | ("dwarf"|"dwarves") | "efreet" | "egg" | "elder" | "eldrazi"
| "elemental" | "elephant" | ("elf"|"elves") | "elk" | "eye" | "faerie" | "ferret" | "fish" | "flagbearer" | "fox"
SUBTYPECREATUREB.5: "frog" | "fungus" | "gargoyle" | "germ" | "giant" | "gnome" | "goat" | "goblin" | "god" | "golem" | "gorgon"
| "graveborn" | "gremlin" | "griffin" | "hag" | "harpy" | "hellion" | "hippo" | "hippogriff" | "homarid" | "homunculus"
| "horror" | "horse" | "hound" | "human" | "hydra" | "hyena" | "illusion" | "imp" | "incarnation" | "insect"
| "jackal" | "jellyfish" | "juggernaut" | "kavu" | "kirin" | "kithkin" | "knight" | "kobold" | "kor" | "kraken"
| "lamia" | "lammasu" | "leech" | "leviathan" | "lhurgoyf" | "licid" | "lizard" | "manticore" | "masticore"
| ("mercenary"|"mercenaries") | "merfolk" | "metathran" | "minion" | "minotaur" | "mole" | "monger" | "mongoose" | "monk"
| "monkey" | "moonfolk" | "mouse" | "mutant" | "myr" | "mystic" | "naga" | "nautilus" | "nephilim" | "nightmare"
| "nightstalker" | "ninja" | "noggle" | "nomad" | "nymph" | ("octopus"|"octopuses") | "ogre" | "ooze" | "orb" | "orc"
| "orgg" | "ouphe" | "ox" | "oyster" | "pangolin" | "pegasus" | "pentavite" | "pest" | "phelddagrif" | "phoenix"
| "pilot" | "pincher" | "pirate" | "plant" | "praetor" | "prism" | "processor" | "rabbit" | "rat" | "rebel"
| "reflection" | "rhino" | "rigger" | "rogue" | "sable" | "salamander" | "samurai" | "sand" | "saproling" | "satyr"
| "scarecrow" | "scion" | "scorpion" | "scout" | "serf" | "serpent" | "servo" | "shade" | "shaman" | "shapeshifter"
| "sheep" | "siren" | "skeleton" | "slith" | "sliver" | "slug" | "snake" | "soldier" | "soltari" | "spawn" | "specter"
| "spellshaper" | "sphinx" | "spider" | "spike" | "spirit" | "splinter" | "sponge" | "squid" | "squirrel" | "starfish"
| "surrakar" | "survivor" | "tetravite" | "thalakos" | "thopter" | "thrull" | "treefolk" | "trilobite" | "triskelavite"
| "troll" | "turtle" | "unicorn" | "vampire" | "vedalken" | "viashino" | "volver" | "wall" | "warrior" | "warlock" | "weird"
| ("werewolf"|"werewolves") | "whale" | "wizard" | ("wolf"|"wolves") | "wolverine" | "wombat" | "worm" | "wraith" | "wurm" | "yeti"
| "zombie" | "zubera"
SUBTYPEPLANAR.100: "alara" | "arkhos" | "azgol" | "belenon" | "bolas’s meditation realm"
| "dominaria" | "equilor" | "ergamon" | "fabacin" | "innistrad" | "iquatana" | "ir"
| "kaldheim" | "kamigawa" | "karsus" | "kephalai" | "kinshala" | "kolbahan" | "kyneth"
| "lorwyn" | "luvion" | "mercadia" | "mirrodin" | "moag" | "mongseng" | "muraganda"
| "new phyrexia" | "phyrexia" | "pyrulea" | "rabiah" | "rath" | "ravnica" | "regatha"
| "segovia" | "serra’s realm" | "shadowmoor" | "shandalar" | "ulgrotha" | "valla"
| "vryn" | "wildfire" | "xerex" | "zendikar"
subtype.5: SUBTYPESPELL | SUBTYPELAND | SUBTYPEARTIFACT | SUBTYPEENCHANTMENT | SUBTYPEPLANESWALKER | SUBTYPECREATUREA | SUBTYPECREATUREB | SUBTYPEPLANAR
SUPERTYPE.5: "basic" | "legendary" | "ongoing" | "snow" | "world"
DAMAGETYPE.100: "damage" | "combat damage" | "excess damage"
ABILITYMODIFIER.5: "mana" | "loyalty"
RELATIVEMODIFIER.100: "next" | "additional" | "extra" | "top" | "bottom" | "new" //Example(s): "an *additional* +1/+1 counter", "take an *extra* turn"
playerstatusmodifier.5: negationofterm? "active" | "defending" //Example(s): "the *active* player", "the *defending* player"
KEYWORDSTATUSMODIFIER.5: "paired" | "kicked" | "face-up" | "face-down" //TODO.5: This will need to be generalized more.
| "fortified" | "monstrous"
effectstatusmodifier.5: reservedverb_pastonly | reservedverb_participleorgerundonly
QUALIFIER.100: "card" | "permanent" | "source" | "spell" | "token" | "effect"
COLOR.5: "white" | "blue" | "black" | "red" | "green" | "monocolored" | "multicolored" | "colorless"
PHASE.100: BEGINNINGPHASE | MAINPHASE | COMBATPHASE | ENDINGPHASE | PHASEGENERIC
BEGINNINGPHASE.5: "beginning phrase"
MAINPHASE.5: ("precombat" | "postcombat")? "main phase"
COMBATPHASE.5: "combat" "phase"?
ENDINGPHASE.5: "ending phase"
PHASEGENERIC.5: "phase"["s"]
STEP.100: UNTAPSTEP | UPKEEPSTEP | DRAWSTEP | BEGINNINGOFCOMBATSTEP
| DECLAREATTACKERSSTEP | DECLAREBLOCKERSSTEP | COMBATDAMAGESTEP | ENDSTEP | CLEANUPSTEP | STEPGENERIC
UNTAPSTEP.5: "untap step"
UPKEEPSTEP.5: "upkeep" "step"?
DRAWSTEP.5: "draw step"
BEGINNINGOFCOMBATSTEP.5: "beginning of combat" "step"?
DECLAREATTACKERSSTEP.5: "declare attackers step"
COMBATDAMAGESTEP.5: "combat damage step"
DECLAREBLOCKERSSTEP.5: "declare blockers step"
ENDSTEP.5: "end step"
CLEANUPSTEP.5: "cleanup step"
STEPGENERIC.5: "step"
TURN.5: "turn"
GAME.5: "game"


//For a list of all the counter types in Magic to date, see here: https://mtg.fandom.com/wiki/Counter_(marker)/Full_List.
countertype.5: "COUNTERTYPE" | ARBITRARYWORD//TODO


ARBITRARYWORD.0: WORD //TODO


//Rules for mana symbols
//TODO.5: Add in text mana descriptions here
manaterm.5: manasymbol
manasymbol.5: "{" manamarkerseq "}"
manamarkerseq.5: manamarker_color -> regularmanasymbol
| NUMBER -> genericmanasymbol
| manamarker_halfmana manamarker_color -> halfmanasymbol
| manamarker_color "/" manamarker_phyrexian -> phyrexianmanasymbol
| manamarker_color "/" manamarker_color -> hybridmanasymbol
| NUMBER "/" manamarker_color -> alternate2manasymbol
| manamarker_snow -> snowmanasymbol
| manamarker_colorless -> colorlessmanasymbol
| manamarker_x -> xmanasymbol
manamarker_halfmana.5: "H"i -> halfmarker
manamarker_color.5: "W"i -> whitemarker
| "U"i -> bluemarker
| "B"i -> blackmarker
| "R"i -> redmarker
| "G"i -> greenmarker
manamarker_snow.5: "S"i -> snowmarker
manamarker_phyrexian.5: "P"i -> phyrexianmarker
manamarker_colorless.5: "C"i -> colorlessmarker
manamarker_x.5: "X"i -> xmarker

//Rules for representing numeric values and math operations
valueterm.5: NUMBER  | CARDINAL ("time"["s"])?| ORDINAL | FREQUENCY | VARIABLEVALUE | valueoperation
FREQUENCY.5: "once" | "twice"
CARDINAL.100: "one" | "two" | "three" | "four" | "five" | "six" | "seven" | "eight" | "nine" | "ten"
| "eleven" | "twelve" | "thirteen" | "fourteen" | "fifteen" | "sixteen" | "seventeen" | "eighteen" | "nineteen" | "twenty" //[TODO]
ORDINAL.5: "first" | "second" | "third" | "fourth" | "fifth" | "sixth" | "seventh" | "eighth" | "ninth" | "tenth" //[TODO]
VARIABLEVALUE.5: "x" | "*"
NUMBERPROPERTY.5: "odd" | "even" | "prime"

//[NOTE.5: Both Scryfall and Mtgjson use a long dash, not a short dash, to indicate a minus on a planeswalker ability]
//PWMINUS.5: "−"
PLUS.5: "+"
MINUS.5: "-"

tapuntapsymbol.5: TAPSYMBOL | UNTAPSYMBOL
TAPSYMBOL.5: "{T}"i
UNTAPSYMBOL.5: "{Q}"i

valueoperation.5: valuecomparisonoperation | valuemathoperation
valuecomparisonoperation.5: "equal to" valueterm // ==
    | "less than" "or equal to"? valueterm // < ; <=
    | "greater than" "or equal to"? valueterm // > ; >=
    | "up to" valueterm // <=
    | valueterm "or" ("less" | "fewer") // <=
    | valueterm "or" ("greater" | "more") // >=
valuemathoperation.5: ("that much" | "that many") //Example: "create *that many* treasure tokens", "draw twice *that many* cards"
| valueterm "rounded" ("up" | "down")
| valueterm "divided" ("evenly" | "as you choose")
| valueterm "plus" valueterm //Example: "the number of Elves you control *plus* the number of Elf cards in your graveyard."
| valueterm "minus" valueterm //Example: "the number of cards in your hand *minus* the number of cards in that player’s hand."