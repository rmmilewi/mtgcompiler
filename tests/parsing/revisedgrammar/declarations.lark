//A regular declaration introduces something that the card relates to or acts on.
//Example(s): "destroy *target nonblack creature*" and "draw *a card*"
//A reference points to a declaration that was made earlier or that is already assumed to exist.
//Example(s) : "draw a card if *that creature* had power 3 or greater" or "target creature blocks *this turn* if able"
declaration.5: singledeclaration | compounddeclaration
compounddeclaration.5:  singledeclaration ("," singledeclaration ",")* CONJUNCTION_OR singledeclaration -> orcompounddeclaration
| singledeclaration ("," singledeclaration ",")* CONJUNCTION_AND singledeclaration -> andcompounddeclaration
| singledeclaration ("," singledeclaration ",")* CONJUNCTION_ANDOR singledeclaration -> andorcompounddeclaration
| CONJUNCTION_NEITHER singledeclaration CONJUNCTION_NOR singledeclaration -> neithernorcompounddeclaration
singledeclaration.5: decorateddeclaration | keywordabilitydeclaration | nakeddeclaration
decorateddeclaration.5: regulardeclaration | referencedeclaration
regulardeclaration.5: declarationdecorator+ declarationdefinition
referencedeclaration.5: declarationdecorator* referencedecorator+ declarationdecorator* declarationdefinition
pronounonlyreferencedeclaration.5: personalpronounreferencedefinition
nakeddeclaration: nakedreferencedeclaration | nakedregulardeclaration //"Naked" declarations are not preceded by decorators (e.g., articles).
nakedreferencedeclaration: personalpronounreferencedefinition | namereferencedefinition
nakedregulardeclaration: declarationdefinition

//Reference definition for third-person personal pronoun references.
//Example(s): "*they* can’t be regenerated." and "Sarkhan the Mad deals damage to *himself*"
personalpronounreferencedefinition.5: PERSONALPRONOUNREFERENCE | SELFPRONOUNREFERENCE | HERAMBIGUOUSREFERENCE
HERAMBIGUOUSREFERENCE.10: "her"i //This could be the possessive form or the object of a sentence.
PERSONALPRONOUNREFERENCE.90: ("he"i | "him"i) | ("she"i) | ("they"i | "them"i) | "it"i | "you"i
SELFPRONOUNREFERENCE.110:  "itself"i | "himself"i | "herself"i | "themselves"i

//Reference form used when when a card makes a reference to itself by name. "~" is a full name reference, "~f" (rarely used) is a first name reference.
//Example(s): "*Chandra, Fire of Kaladesh (~)* deals 1 damage to target player or planeswalker. If *Chandra (~f)* has dealt 3 or more damage [...]"
namereferencedefinition.5: NAMEREFSYMBOL
NAMEREFSYMBOL.5: "~" | "~f"

//Reference decorators help clarify what is being talked about. Note that reference decorators can have nested
//references to other defined things (like "your *opponent's* hand").
//Example(s): "Destroy target creature. *Its* controller [...]" and "*its owner’s* hand." "*that creature’s* toughness."
referencedecorator.5: thirdpersonimpersonalreference | POSSESSIVEPRONOUNREFERNCE | HERAMBIGUOUSREFERENCE | "which"i
thirdpersonimpersonalreference.5: (PRONOUN_THAT | PRONOUN_THOSE) | (PRONOUN_THIS | PRONOUN_THESE)
PRONOUN_THAT.5: "that"i
PRONOUN_THOSE.5: "those"i
PRONOUN_THIS.5: "this"i
PRONOUN_THESE.5 : "these"i

POSSESSIVEPRONOUNREFERNCE.150: "its"i | "your"i | "their"i | "his"i //Note.5: "her"i is handled separately to capture the ambiguity.

//Declaration decorators. The most popular decorator is "target".
//Example(s): "destroy *target* nonblack creature", "*any* permanent"
declarationdecorator.5: "each"i PREPOSITION_OF? -> eachdecorator
| "same"i -> samedecorator
| "all"i -> alldecorator
| "other"i -> otherdecorator
| "another"i -> anotherdecorator
| ("a"i | "an"i) -> indefinitearticledecorator
| "the"i -> definitearticledecorator
| valueterm? TARGETPRESENT -> targetdecorator
| "any"i -> anydecorator
| ONLY FREQUENCY? -> onlydecorator //Example: "*Only twice* each turn", "*only* your opponents"

//A declaration definition is made up of a set of descriptive terms like "a *blue permanent*" or "*two 1/1 green elf creature tokens*".
//There can be limited nesting of declarations inside definitions like with possessives ("its *owner's* hand") and prepositions
//("a creature *with a +1/+1 counter* *on it*").
declarationdefinition.5: declarationdefinition_standardorder
declarationdefinition_standardorder.10: possessiveclarationreference* valueterm? powertoughnessterm? modifierterm* (colorterm | damageterm | playerterm | typeterm | powertoughnessterm | loyaltycostterm | tapuntapsymbol | zoneterm | timeterm | valueterm | characteristicterm | qualifierterm | cointerm | diceterm | variableterm | quotedabilityterm | targetasnoun)+ (prepositionalattacheddeclaration | declarationverbexpression)*
| anytargetterm
| valueterm? counterterm
| nameterm
| specialterm
| possessiveclarationreference* CONTROLPRESENT prepositionalattacheddeclaration* //Example(s): Control here is used as a noun, like "under your *control*", "gain *control* of [...]"
targetasnoun.5: TARGETPRESENT //Example(s): "~ deals 2 damage divided as you choose among one or two *targets*."
declarationverbexpression: PRONOUN_THAT? (decorateddeclaration | nakedreferencedeclaration) (reservedverbverb | simpleverb) //TODO: This likely causes an ambiguity conflict with expressions. Who owns "destroy target creature *you control*", the destroy expression or the target creature declaration?
| PRONOUN_THAT? (reservedverbverb | simpleverb) "not"? (decorateddeclaration | nakedreferencedeclaration)




//TODO: Temporarily expanded prepositionalattacheddeclaration to include expressions like 'damage that *would be dealt*'. Need to clean up this rule.
prepositionalattacheddeclaration.5: declarationpreposition expressionordeclaration
declarationpreposition.2: ONLY? (PREPOSITION_OF | PREPOSITION_ON | PREPOSITION_WHO | PREPOSITION_IN | PREPOSITION_UNDER | PREPOSITION_WITHOUT | PREPOSITION_WITH | PREPOSITION_AS | PREPOSITION_EXCEPT | PREPOSITION_TO | PRONOUN_THAT)
possessiveclarationreference.5: (declarationdefinition_standardorder | namereferencedefinition) ("'s"|"'")
specialterm.5: MECHANICSPECIFICTERM
MECHANICSPECIFICTERM.5: "ring"i //Example: "the *ring* has tempted you two or more times this game"
| "dungeon"i //Example: "venture into the *dungeon*"
| "city's blessing"i //Example: "As long as you have the *city’s blessing*"
| "evidence"i //Example: "If you collected *evidence*"


//Example(s): "a 1/1 red Mercenary creature token with *"{T}: Target creature you control gets +1/+0 until end of turn. Activate only as a sorcery."*
quotedabilityterm.5: /"/ statementblock /"/

negationofterm.5: NONPREFIX
NONPREFIX.5: "non"i ("-")?
typeterm.5: negationofterm? (TYPE ["s"i] | SUBTYPESPELL ["s"i] | SUBTYPELAND ["s"i] | SUBTYPEARTIFACT ["s"i] | SUBTYPEENCHANTMENT ["s"i] | SUBTYPEPLANESWALKER | SUBTYPECREATUREA ["s"i] | SUBTYPECREATUREB ["s"i] | SUBTYPEPLANAR | SUPERTYPE)
modifierterm.5: negationofterm? (ABILITYMODIFIER | KEYWORDSTATUSMODIFIER  | RELATIVEMODIFIER | effectstatusmodifier | playerstatusmodifier)
qualifierterm.5: negationofterm? QUALIFIER["s"i]
timeterm.5: negationofterm? (PHASE | STEP | TURN | GAME)
playerterm.5: PLAYER["s"i]
zoneterm.5: ZONE
powertoughnessterm.5: valueterm "/" valueterm

//NOTE: Planeswalker loyalty costs are sometimes written with brackets when its not a cost (like with Carth the Lion),
//but without brackets on planeswalker cards.
loyaltycostterm.5: "[" ((SIGNED_INT | "0") | ((PLUS | MINUS) VARIABLEVALUESYMBOL)) "]" | ((SIGNED_INT | "0") | ((PLUS | MINUS) VARIABLEVALUESYMBOL))
counterterm.5: (countertype | powertoughnessterm) COUNTERPRESENT
characteristicterm.5: negationofterm? (OBJECTCHARACTERISTIC | PLAYERCHARACTERISTIC)
nameterm.5: "OBJECTNAME"i | ARBITRARYWORD ","? ARBITRARYWORD* //TODO: Remove "OBJECTNAME" placeholder
damageterm.5: negationofterm? DAMAGETYPE
colorterm.5: negationofterm? COLOR

//Rules for terms that don't stand by themselves but refer to something else on the card, like "this *way* (the way just described)"
//Or "if *able* (able to block)".
variableterm.20: "beginning"i //Example: "the *beginning* of your next upkeep"
| "both"i //Example(s): "you may choose *both*"
| "choice"i["s"i] //Example(s): "their *choice*", "of your *choice*"
| "end"i //Example: "until *end* of turn"
| "order"i //Example: "Put that card onto the battlefield and the rest on the bottom of your library in a random *order*."
| "pile"i["s"i] //Example: "target opponent looks at the top three cards of your library and separates them into a face-down pile and a face-up pile."
| "rest"i //Example: "You have no maximum hand size for the *rest* of the game."
| "result"i["s"] //Example: "draw cards equal to the *result*"
| "time"i["s"i] //Example(s): "the number of *times* ~ was kicked", "the first time this turn"
| VOTEPRESENT //Example(s): "each *vote* they receive"
| "way"i //Example: "Creatures destroyed this *way* can't be regenerated."
| COPYPRESENT //Example(s): "You may have Shapeshifters you control become *copies* of that creature", "choose new targets for the *copy*"
| "able"i//Example(s): "Target creature blocks this turn if *able*"
| ADDITION //Example(s): "This creature is the chosen type in *addition* to its other types." //TODO: This may be the wrong place to put "addition".
anytargetterm.5: "any target"i //Special nullary variant like "~ deals three damage to *any target*"
ADDITION.10: "addition"i


//Some cards instruct you to roll dice and/or flip coins. These rules cover those declarations.
cointerm.5: "coin"i["s"i] //Example: "flip three *coins*"
diceterm.5: DIE //Example: "roll a *d20*", "roll 3*d6*"
DIE.5: "d" INT

//Keyworded ability declaration
//Regular keyworded abilities are those that are of the form "keyword (cost/description"?.
//Note that the parser will allow for illegal keyworded ability definitions like "flying 3", and it's the
//job of the semantic analyzer to check for things like this. Special keyworded abilities, meanwhile, are
//those that have very particular syntax around them and its easier to spell them out in a special rule.
keywordabilitydeclaration.5: regularkeywordedabilitydefinition | specialkeywordedabilitydefinition

regularkeywordedabilitydefinition.5: regularkeywordedability declaration? //Example(s): "Flying"i, "Bushido 3", "Champion a Kithkin"
| regularkeywordedability declaration DASH declaration //Example(s): "Impending 4—{2}{R}{R}"
| regularkeywordedability DASH expressionordeclaration //Example(s): "Ward—Pay 7 life"

//special cases include...
//TODO: landwalk, hexproof from X and from Y, [typeexpression] "cycling"i cost, "splice"i "onto"i typeexpression cost, typeexpression "offering"i
//TODO: "forecast"i activationstatement, "aura swap" (if we wanted to generalize to TYPE swap), banding, protection
//TODO: partner with objectname, kwcompanion.5: "companion"i, "craft"i "with"i declarationorreference cost, spree (Choose one or more additional costs.)
specialkeywordedabilitydefinition.5: specialkeywordedabilitydefinition_hexproof | specialkeywordedabilitydefinition_landwalk | specialkeywordedabilitydefinition_cycling
| specialkeywordedabilitydefinition_auraswap | specialkeywordedabilitydefinition_forecast | specialkeywordedabilitydefinition_affinity
| specialkeywordedabilitydefinition_partner | specialkeywordedabilitydefinition_companion | specialkeywordedabilitydefinition_craft
| specialkeywordedabilitydefinition_spree | specialkeywordedabilitydefinition_banding | specialkeywordedabilitydefinition_protection

//Example(s): "Hexproof", "Hexproof from", "Hexproof from artifacts, creatures, and enchantments"
specialkeywordedabilitydefinition_hexproof.5: KEYWORDEDABILITY_HEXPROOF (PREPOSITION_FROM declaration (CONJUNCTION_AND PREPOSITION_FROM declaration)? )?

//Example(s): "Target creature loses all *landwalk* abilities until end of turn." (generic reference to landwalk abilities), "Islandwalk", "Legendary landwalk",
specialkeywordedabilitydefinition_landwalk.5: typeterm+ "walk"i

//Example(s): "Cycling {2}", "Swampcycling {2}", "Cycling—Sacrifice a land."
specialkeywordedabilitydefinition_cycling.5: typeterm* CYCLEPARTICIPLEORGERUND (declaration | DASH expression)

//Example(s): "Aura swap {2}{U} ({2}{U}: Exchange this Aura with an Aura card in your hand.)"
specialkeywordedabilitydefinition_auraswap.5: typeterm "swap"i

//Example(s): "Forecast — {2}{W}, Reveal Piercing Rays from your hand: Tap target untapped creature."
specialkeywordedabilitydefinition_forecast: KEYWORDEDABILITY_FORECAST DASH activationstatement

//Example(s): Affinity for artifacts", "Affinity for historic permanents"
specialkeywordedabilitydefinition_affinity: KEYWORDEDABILITY_AFFINITY PREPOSITION_FOR declaration

//Example(s): "Partner (You can have two commanders if both have partner.)", "Partner with Amy Pond"
specialkeywordedabilitydefinition_partner: KEYWORDEDABILITY_PARTNER (PREPOSITION_WITH nameterm)?

//Example(s): "Companion — Each creature card in your starting deck is a Cat, Elemental, Nightmare, Dinosaur, or Beast card.", "Doctor's Companion"
specialkeywordedabilitydefinition_companion: KEYWORDEDABILITY_COMPANION DASH statementblock | "Doctor's"i KEYWORDEDABILITY_COMPANION

//Example(s): "Craft with one or more creatures {2}{B}{B}", "Craft with four or more red instant and/or sorcery cards {3}{R}{R}"
specialkeywordedabilitydefinition_craft: KEYWORDEDABILITY_CRAFT PREPOSITION_WITH declaration manaterm+

//Example(s): "Spree (Choose one or more additional costs.)"
//"+ {2} — Return target creature card from your graveyard to the battlefield."
//"+ {1} — Return target Mount or Vehicle card from your graveyard to the battlefield."
//"+ {1} — Return target Aura or Equipment card from your graveyard to the battlefield attached to a creature you control."
specialkeywordedabilitydefinition_spree: KEYWORDEDABILITY_SPREE (NEWLINE spreestatement)+
spreestatement: PLUS declaration DASH statementblock

//Example: "Banding", "Green legendary creatures you control have 'bands with other legendary creatures'.", "Target creature loses banding and all 'bands with other' abilities until end of turn." (Tolaria)
specialkeywordedabilitydefinition_banding: KEYWORDEDABILITY_BANDING | "bands"i PREPOSITION_WITH declaration

//Example: "protection from blue", protection from everything", ""Permanents your opponents control lose hexproof, indestructible, *protection*, shroud, and ward until end of turn."
specialkeywordedabilitydefinition_protection: KEYWORDEDABILITY_PROTECTION (PREPOSITION_FROM declaration)?


regularkeywordedability.15: KEYWORDEDABILITY_ABSORB | KEYWORDEDABILITY_AFFLICT | KEYWORDEDABILITY_AFTERLIFE | KEYWORDEDABILITY_AFTERMATH
| KEYWORDEDABILITY_AMPLIFY | KEYWORDEDABILITY_ANNIHILATOR | KEYWORDEDABILITY_ASCEND | KEYWORDEDABILITY_ASSIST | KEYWORDEDABILITY_AURASWAP
| KEYWORDEDABILITY_AWAKEN | KEYWORDEDABILITY_BACKUP | KEYWORDEDABILITY_BARGAIN | KEYWORDEDABILITY_BATTLECRY | KEYWORDEDABILITY_BESTOW
| KEYWORDEDABILITY_BLITZ | KEYWORDEDABILITY_BLOODTHIRST | KEYWORDEDABILITY_BOAST | KEYWORDEDABILITY_BUSHIDO | KEYWORDEDABILITY_BUYBACK
| KEYWORDEDABILITY_CASCADE | KEYWORDEDABILITY_CASUALTY | KEYWORDEDABILITY_CHAMPION | KEYWORDEDABILITY_CHANGELING | KEYWORDEDABILITY_CIPHER
| KEYWORDEDABILITY_CLEAVE | KEYWORDEDABILITY_COMPLEATED | KEYWORDEDABILITY_CONSPIRE | KEYWORDEDABILITY_CONVOKE | KEYWORDEDABILITY_CREW
| KEYWORDEDABILITY_CUMULATIVEUPKEEP | KEYWORDEDABILITY_DASH | KEYWORDEDABILITY_DAYANDNIGHTBOUND | KEYWORDEDABILITY_DEATHTOUCH | KEYWORDEDABILITY_DECAYED
| KEYWORDEDABILITY_DEFENDER | KEYWORDEDABILITY_DELVE | KEYWORDEDABILITY_DEMONSTRATE | KEYWORDEDABILITY_DETHRONE | KEYWORDEDABILITY_DEVOID
| KEYWORDEDABILITY_DEVOUR | KEYWORDEDABILITY_DISGUISE | KEYWORDEDABILITY_DISTURB | KEYWORDEDABILITY_DOUBLESTRIKE | KEYWORDEDABILITY_DREDGE
| KEYWORDEDABILITY_ECHO | KEYWORDEDABILITY_EMBALM | KEYWORDEDABILITY_EMERGE | keywordedability_enchant | KEYWORDEDABILITY_ENCORE
| KEYWORDEDABILITY_ENLIST | KEYWORDEDABILITY_ENTWINE | KEYWORDEDABILITY_EPIC | keywordedability_equip | KEYWORDEDABILITY_ESCALATE
| KEYWORDEDABILITY_ESCAPE | KEYWORDEDABILITY_ETERNALIZE | KEYWORDEDABILITY_EVOKE | KEYWORDEDABILITY_EVOLVE | KEYWORDEDABILITY_EXALTED
| KEYWORDEDABILITY_EXPLOIT | KEYWORDEDABILITY_EXTORT | KEYWORDEDABILITY_FABRICATE | KEYWORDEDABILITY_FADING | KEYWORDEDABILITY_FEAR
| KEYWORDEDABILITY_FIRSTSTRIKE | KEYWORDEDABILITY_FLANKING | KEYWORDEDABILITY_FLASH | KEYWORDEDABILITY_FLASHBACK | KEYWORDEDABILITY_FLYING
| KEYWORDEDABILITY_FORETELL | KEYWORDEDABILITY_FORMIRRODIN | KEYWORDEDABILITY_FORTIFY | KEYWORDEDABILITY_FRENZY | KEYWORDEDABILITY_FUSE
| KEYWORDEDABILITY_GIFT | KEYWORDEDABILITY_GRAFT | KEYWORDEDABILITY_GRAVESTORM | KEYWORDEDABILITY_HASTE | KEYWORDEDABILITY_HAUNT
| KEYWORDEDABILITY_HIDDENAGENDA | KEYWORDEDABILITY_HIDEAWAY | KEYWORDEDABILITY_HORSEMANSHIP | KEYWORDEDABILITY_IMPENDING | KEYWORDEDABILITY_IMPROVISE
| KEYWORDEDABILITY_INDESTRUCTIBLE | KEYWORDEDABILITY_INFECT | KEYWORDEDABILITY_INGEST | KEYWORDEDABILITY_INTIMIDATE | KEYWORDEDABILITY_JUMPSTART
| KEYWORDEDABILITY_KICKER | KEYWORDEDABILITY_LEVELUP | KEYWORDEDABILITY_LIFELINK | KEYWORDEDABILITY_LIVINGWEAPON | KEYWORDEDABILITY_LIVINGMETAL
| KEYWORDEDABILITY_MADNESS | KEYWORDEDABILITY_MELEE | KEYWORDEDABILITY_MENACE | KEYWORDEDABILITY_MENTOR | KEYWORDEDABILITY_MIRACLE
| KEYWORDEDABILITY_MODULAR | KEYWORDEDABILITY_MORPH | KEYWORDEDABILITY_MUTATE | KEYWORDEDABILITY_MYRIAD | KEYWORDEDABILITY_NINJUTSU
| KEYWORDEDABILITY_OFFSPRING | KEYWORDEDABILITY_OUTLAST | KEYWORDEDABILITY_OVERLOAD | KEYWORDEDABILITY_PERSIST | KEYWORDEDABILITY_PHASING
| KEYWORDEDABILITY_PLOT | KEYWORDEDABILITY_POISONOUS | KEYWORDEDABILITY_PROTOTYPE | KEYWORDEDABILITY_PROVOKE | KEYWORDEDABILITY_PROWESS
| KEYWORDEDABILITY_PROWL | KEYWORDEDABILITY_RAMPAGE | KEYWORDEDABILITY_RAVENOUS | KEYWORDEDABILITY_REACH | KEYWORDEDABILITY_READAHEAD
| KEYWORDEDABILITY_REBOUND | KEYWORDEDABILITY_RECONFIGURE | KEYWORDEDABILITY_RECOVER | KEYWORDEDABILITY_REINFORCE | KEYWORDEDABILITY_RENOWN
| KEYWORDEDABILITY_RETRACE | KEYWORDEDABILITY_RIOT | KEYWORDEDABILITY_RIPPLE | KEYWORDEDABILITY_SADDLE | KEYWORDEDABILITY_SCAVENGE
| KEYWORDEDABILITY_SHADOW | KEYWORDEDABILITY_SHROUD | KEYWORDEDABILITY_SKULK | KEYWORDEDABILITY_SOULBOND | KEYWORDEDABILITY_SOULSHIFT
| KEYWORDEDABILITY_SPECTACLE | KEYWORDEDABILITY_SPLITSECOND | KEYWORDEDABILITY_SQUAD | KEYWORDEDABILITY_STORM | KEYWORDEDABILITY_SUNBURST
| KEYWORDEDABILITY_SURGE | keywordedability_suspend | KEYWORDEDABILITY_TIMETRAVEL | KEYWORDEDABILITY_TOTEMARMOR | KEYWORDEDABILITY_TOXIC
| KEYWORDEDABILITY_TRAINING | KEYWORDEDABILITY_TRAMPLE | KEYWORDEDABILITY_TRANSFIGURE | KEYWORDEDABILITY_TRANSMUTE | KEYWORDEDABILITY_TRIBUTE
| KEYWORDEDABILITY_UNDAUNTED | KEYWORDEDABILITY_UNDYING | KEYWORDEDABILITY_UNEARTH | KEYWORDEDABILITY_UNLEASH | KEYWORDEDABILITY_VANISHING
| KEYWORDEDABILITY_VIGILANCE | KEYWORDEDABILITY_WARD | KEYWORDEDABILITY_WITHER

KEYWORDEDABILITY_ABSORB.5: "absorb"i
KEYWORDEDABILITY_AFFINITY.5: "affinity"i
KEYWORDEDABILITY_AFFLICT.5: "afflict"i
KEYWORDEDABILITY_AFTERLIFE.5: "afterlife"i
KEYWORDEDABILITY_AFTERMATH.5: "aftermath"i
KEYWORDEDABILITY_AMPLIFY.5: "amplify"i
KEYWORDEDABILITY_ANNIHILATOR.5: "annihilator"i
KEYWORDEDABILITY_ASCEND.5: "ascend"i
KEYWORDEDABILITY_ASSIST.5: "assist"i
KEYWORDEDABILITY_AURASWAP.5: "aura swap"i
KEYWORDEDABILITY_AWAKEN.5: "awaken"i
KEYWORDEDABILITY_BACKUP.5: "backup"i
KEYWORDEDABILITY_BANDING.5: "banding"i
KEYWORDEDABILITY_BARGAIN.5: "bargain"i
KEYWORDEDABILITY_BATTLECRY.5: "battle cry"i
KEYWORDEDABILITY_BESTOW.5: "bestow"i
KEYWORDEDABILITY_BLITZ.5: "blitz"i
KEYWORDEDABILITY_BLOODTHIRST.5: "bloodthirst"i
KEYWORDEDABILITY_BOAST.5: "boast"i
KEYWORDEDABILITY_BUSHIDO.5: "bushido"i
KEYWORDEDABILITY_BUYBACK.5: "buyback"i
KEYWORDEDABILITY_CASCADE.5: "cascade"i
KEYWORDEDABILITY_CASUALTY.5: "casualty"i
KEYWORDEDABILITY_CHAMPION.5: "champion"i
KEYWORDEDABILITY_CHANGELING.5: "changeling"i
KEYWORDEDABILITY_CIPHER.5: "cipher"i
KEYWORDEDABILITY_CLEAVE.5: "cleave"i
KEYWORDEDABILITY_COMPANION.5: "companion"i
KEYWORDEDABILITY_COMPLEATED.5: "compleated"i
KEYWORDEDABILITY_CONSPIRE.5: "conspire"i
KEYWORDEDABILITY_CONVOKE.5: "convoke"i
KEYWORDEDABILITY_CRAFT.5: "craft"i
KEYWORDEDABILITY_CREW.5: "crew"i
KEYWORDEDABILITY_CUMULATIVEUPKEEP.5: "cumulative upkeep"i
KEYWORDEDABILITY_DASH.5: "dash"i
KEYWORDEDABILITY_DAYANDNIGHTBOUND.5: ("day"i | "night"i) "bound"i
KEYWORDEDABILITY_DEATHTOUCH.5: "deathtouch"i
KEYWORDEDABILITY_DECAYED.5: "decayed"i
KEYWORDEDABILITY_DEFENDER.5: "defender"i
KEYWORDEDABILITY_DELVE.5: "delve"i
KEYWORDEDABILITY_DEMONSTRATE.5: "demonstrate"i
KEYWORDEDABILITY_DETHRONE.5: "dethrone"i
KEYWORDEDABILITY_DEVOID.5: "devoid"i
KEYWORDEDABILITY_DEVOUR.5: "devour"i
KEYWORDEDABILITY_DISGUISE.5: "disguise"i
KEYWORDEDABILITY_DISTURB.5: "disturb"i
KEYWORDEDABILITY_DOUBLESTRIKE.5: "double strike"i
KEYWORDEDABILITY_DREDGE.5: "dredge"i
KEYWORDEDABILITY_ECHO.5: "echo"i
KEYWORDEDABILITY_EMBALM.5: "embalm"i
KEYWORDEDABILITY_EMERGE.5: "emerge"i
keywordedability_enchant.5: ENCHANTPRESENT
KEYWORDEDABILITY_ENCORE.5: "encore"i
KEYWORDEDABILITY_ENLIST.5: "enlist"i
KEYWORDEDABILITY_ENTWINE.5: "entwine"i
KEYWORDEDABILITY_EPIC.5: "epic"i
keywordedability_equip.5: EQUIPPRESENT
KEYWORDEDABILITY_ESCALATE.5: "escalate"i
KEYWORDEDABILITY_ESCAPE.5: "escape"i
KEYWORDEDABILITY_ETERNALIZE.5: "eternalize"i
KEYWORDEDABILITY_EVOKE.5: "evoke"i
KEYWORDEDABILITY_EVOLVE.5: "evolve"i
KEYWORDEDABILITY_EXALTED.5: "exalted"i
KEYWORDEDABILITY_EXPLOIT.5: "exploit"i
KEYWORDEDABILITY_EXTORT.5: "extort"i
KEYWORDEDABILITY_FABRICATE.5: "fabricate"i
KEYWORDEDABILITY_FADING.5: "fading"i
KEYWORDEDABILITY_FEAR.5: "fear"i
KEYWORDEDABILITY_FIRSTSTRIKE.5: "first strike"i
KEYWORDEDABILITY_FLANKING.5: "flanking"i
KEYWORDEDABILITY_FLASH.5: "flash"i
KEYWORDEDABILITY_FLASHBACK.5: "flashback"i
KEYWORDEDABILITY_FLYING.5: "flying"i
KEYWORDEDABILITY_FORECAST.5: "forecast"i
KEYWORDEDABILITY_FORETELL.5: "foretell"i
KEYWORDEDABILITY_FORMIRRODIN.5: "for mirrodin!"i
KEYWORDEDABILITY_FORTIFY.5: "fortify"i
KEYWORDEDABILITY_FRENZY.5: "frenzy"i
KEYWORDEDABILITY_FUSE.5: "fuse"i
KEYWORDEDABILITY_GIFT.5: GIFTPRESENT
KEYWORDEDABILITY_GRAFT.5: "graft"i
KEYWORDEDABILITY_GRAVESTORM.5: "gravestorm"i
KEYWORDEDABILITY_HASTE.5: "haste"i
KEYWORDEDABILITY_HAUNT.5: "haunt"i
KEYWORDEDABILITY_HEXPROOF.200: "hexproof"i
KEYWORDEDABILITY_HIDDENAGENDA.5: "hidden agenda"i
KEYWORDEDABILITY_HIDEAWAY.5: "hideaway"i
KEYWORDEDABILITY_HORSEMANSHIP.5: "horsemanship"i
KEYWORDEDABILITY_IMPENDING.5: "impending"i
KEYWORDEDABILITY_IMPROVISE.5: "improvise"i
KEYWORDEDABILITY_INDESTRUCTIBLE.5: "indestructible"i
KEYWORDEDABILITY_INFECT.5: "infect"i
KEYWORDEDABILITY_INGEST.5: "ingest"i
KEYWORDEDABILITY_INTIMIDATE.5: "intimidate"i
KEYWORDEDABILITY_JUMPSTART.5: "jump-start"i
KEYWORDEDABILITY_KICKER.5: "multi"i? "kicker"i
KEYWORDEDABILITY_LEVELUP.5: "level up"i
KEYWORDEDABILITY_LIFELINK.20: "lifelink"i
KEYWORDEDABILITY_LIVINGWEAPON.5: "living weapon"i
KEYWORDEDABILITY_LIVINGMETAL.5: "living metal"i
KEYWORDEDABILITY_MADNESS.5: "madness"i
KEYWORDEDABILITY_MELEE.5: "melee"i
KEYWORDEDABILITY_MENACE.5: "menace"i
KEYWORDEDABILITY_MENTOR.5: "mentor"i
KEYWORDEDABILITY_MIRACLE.5: "miracle"i
KEYWORDEDABILITY_MODULAR.5: "modular"i
KEYWORDEDABILITY_MORPH.5: "mega"i? "morph"i
KEYWORDEDABILITY_MUTATE.5: "mutate"i
KEYWORDEDABILITY_MYRIAD.5: "myriad"i
KEYWORDEDABILITY_NINJUTSU.5: "ninjutsu"i
KEYWORDEDABILITY_OFFSPRING.5: "offspring"i
KEYWORDEDABILITY_OUTLAST.5: "outlast"i
KEYWORDEDABILITY_OVERLOAD.5: "overload"i
KEYWORDEDABILITY_PARTNER.5: "partner"i
KEYWORDEDABILITY_PERSIST.5: "persist"i
KEYWORDEDABILITY_PHASING.5: "phasing"i
KEYWORDEDABILITY_PLOT.5: "plot"i
KEYWORDEDABILITY_POISONOUS.5: "poisonous"i
KEYWORDEDABILITY_PROTECTION.5: "protection"i
KEYWORDEDABILITY_PROTOTYPE.5: "prototype"i
KEYWORDEDABILITY_PROVOKE.5: "provoke"i
KEYWORDEDABILITY_PROWESS.5: "prowess"i
KEYWORDEDABILITY_PROWL.5: "prowl"i
KEYWORDEDABILITY_RAMPAGE.5: "rampage"i
KEYWORDEDABILITY_RAVENOUS.5: "ravenous"i
KEYWORDEDABILITY_REACH.5: "reach"i
KEYWORDEDABILITY_READAHEAD.5: "read ahead"i
KEYWORDEDABILITY_REBOUND.5: "rebound"i
KEYWORDEDABILITY_RECONFIGURE.5: "reconfigure"i
KEYWORDEDABILITY_RECOVER.5: "recover"i
KEYWORDEDABILITY_REINFORCE.5: "reinforce"i
KEYWORDEDABILITY_RENOWN.5: "renown"i
KEYWORDEDABILITY_RETRACE.5: "retrace"i
KEYWORDEDABILITY_RIOT.5: "riot"i
KEYWORDEDABILITY_RIPPLE.5: "ripple"i
KEYWORDEDABILITY_SADDLE.5: "saddle"i
KEYWORDEDABILITY_SCAVENGE.5: "scavenge"i
KEYWORDEDABILITY_SHADOW.5: "shadow"i
KEYWORDEDABILITY_SHROUD.5: "shroud"i
KEYWORDEDABILITY_SKULK.5: "skulk"i
KEYWORDEDABILITY_SOULBOND.5: "soulbond"i
KEYWORDEDABILITY_SOULSHIFT.5: "soulshift"i
KEYWORDEDABILITY_SPECTACLE.5: "spectacle"i
KEYWORDEDABILITY_SPLITSECOND.5: "split second"i
KEYWORDEDABILITY_SPREE.5: "spree"i
KEYWORDEDABILITY_SQUAD.5: "squad"i
KEYWORDEDABILITY_STORM.5: "storm"i
KEYWORDEDABILITY_SUNBURST.5: "sunburst"i
KEYWORDEDABILITY_SURGE.5: "surge"i
keywordedability_suspend.5: SUSPENDPRESENT
KEYWORDEDABILITY_TIMETRAVEL.5: "time travel"i
KEYWORDEDABILITY_TOTEMARMOR.5: "totem armor"i
KEYWORDEDABILITY_TOXIC.5: "toxic"i
KEYWORDEDABILITY_TRAINING.5: "training"i
KEYWORDEDABILITY_TRAMPLE.5: "trample"i
KEYWORDEDABILITY_TRANSFIGURE.5: "transfigure"i
KEYWORDEDABILITY_TRANSMUTE.5: "transmute"i
KEYWORDEDABILITY_TRIBUTE.5: "tribute"i
KEYWORDEDABILITY_UNDAUNTED.5: "undaunted"i
KEYWORDEDABILITY_UNDYING.5: "undying"i
KEYWORDEDABILITY_UNEARTH.5: "unearth"i
KEYWORDEDABILITY_UNLEASH.5: "unleash"i
KEYWORDEDABILITY_VANISHING.5: "vanishing"i
KEYWORDEDABILITY_VIGILANCE.5: "vigilance"i
KEYWORDEDABILITY_WARD.5: "ward"i
KEYWORDEDABILITY_WITHER.5: "wither"i


//Below are hardcoded game terms used in declarations.
PLAYER.10: "player"i | "opponent"i |  "teammate"i | "team"i | "controller"i | "owner"i
PLAYERCHARACTERISTIC.10: "maximum hand size" | "life total"["s"i] | "life"i | "cards in hand" | "deck"i | "priority"i
OBJECTCHARACTERISTIC.5: "card"i? NAMEPRESENT | "mana value"["s"i] | "color"i["s"i] | "color indicator"["s"i] | "type"i["s"i] | "card type"["s"i] | "subtype"i["s"i] | "supertype"i["s"i]
| "rules text" | ("ability"i | "abilities"i ) | "power"i | "toughness"i | "base power" | "base toughness" | "loyalty"i | "hand modifier" | "life modifier"
ZONE.10: "battlefield"i | "graveyard"i["s"i] | ("library"i|"libraries"i) | "hand"i["s"i] | "stack"i | EXILEPRESENT | "command zone" | "outside the game" | "anywhere"i
TYPE.10: "planeswalker"i | "conspiracy"i | "creature"i | "enchantment"i | "instant"i | "land"i | "phenomenon"i | "plane"i | "artifact"i | "scheme"i | "sorcery"i | "tribal"i | "vanguard"i
SUBTYPESPELL.10: "arcane"i | "trap"i | "adventure"i
SUBTYPELAND.10: "desert"i | "forest"i | "gate"i | "island"i | "lair"i | "locus"i
| "mine"i | "mountain"i | "plains"i | "power-plant" | "swamp"i | "tower"i | "urza's"
SUBTYPEARTIFACT.10: "clue"i | "contraption"i | "equipment"i | "fortification"i | "treasure"i | "vehicle"i | "food"i
SUBTYPEENCHANTMENT.10: "aura"i | "cartouche"i | "curse"i | "saga"i | "shrine"i | "rune"i | "shard"i | "case"i | "room"i
SUBTYPEPLANESWALKER.10: "ajani"i | "aminatou"i | "angrath"i | "arlinn"i | "ashiok"i | "bolas"i | "chandra"i
| "dack"i | "daretti"i | "domri"i | "dovin"i | "elspeth"i | "estrid"i | "freyalise"i | "garruk"i | "gideon"i
| "huatli"i | "jace"i | "jaya"i | "karn"i | "kaya"i | "kiora"i | "koth"i | "liliana"i | "nahiri"i | "narset"i
| "nissa"i | "nixilis"i | "ral"i | "rowan"i | "saheeli"i | "samut"i | "sarkhan"i | "sorin"i | "tamiyo"i | "teferi"i
| "tezzeret"i | "tibalt"i | "ugin"i | "venser"i | "vivien"i | "vraska"i | "will"i | "windgrace"i | "xenagos"i
| "yanggu"i | "yanling"i
//[TODO.5: SUBTYPECREATUREA and SUBTYPECREATUREB are split up because having such a long list of alternatives apparently]
//[causes Lark to suffer a recursion depth error. We should see if this is fixable.]
SUBTYPECREATUREA.10: "advisor"i | "aetherborn"i | ("ally"i|"allies"i) | "angel"i | "antelope"i | "ape"i | "archer"i | "archon"i
| "artificer"i | "assassin"i | "assembly-worker" | "atog"i | "aurochs"i | "avatar"i | "azra"i | "badger"i
| "barbarian"i | "basilisk"i | "bat"i | "bear"i | "beast"i | "beeble"i | "berserker"i | "bird"i | "blinkmoth"i
| "boar"i | "bringer"i | "brushwagg"i | "camarid"i | "camel"i | "caribou"i | "carrier"i | "cat"i | "centaur"i
| "cephalid"i | "chimera"i | "citizen"i | "cleric"i | "cockatrice"i | "construct"i | "coward"i | "crab"i
| "crocodile"i | "cyclops"i | "dauthi"i | "demon"i | "deserter"i | "detective"i | "devil"i | "dinosaur"i | "djinn"i | "dragon"i
| "drake"i | "dreadnought"i | "drone"i | "druid"i | "dryad"i | ("dwarf"i|"dwarves"i) | "efreet"i | "egg"i | "elder"i | "eldrazi"i
| "elemental"i | "elephant"i | ("elf"i|"elves"i) | "elk"i | "eye"i | "faerie"i | "ferret"i | "fish"i | "flagbearer"i | "fox"i
SUBTYPECREATUREB.5: "frog"i | "fungus"i | "gargoyle"i | "germ"i | "giant"i | "gnome"i | "goat"i | "goblin"i | "god"i | "golem"i | "gorgon"i
| "graveborn"i | "gremlin"i | "griffin"i | "hag"i | "harpy"i | "hellion"i | "hippo"i | "hippogriff"i | "homarid"i | "homunculus"i
| "horror"i | "horse"i | "hound"i | "human"i | "hydra"i | "hyena"i | "illusion"i | "imp"i | "incarnation"i | "insect"i
| "jackal"i | "jellyfish"i | "juggernaut"i | "kavu"i | "kirin"i | "kithkin"i | "knight"i | "kobold"i | "kor"i | "kraken"i
| "lamia"i | "lammasu"i | "leech"i | "leviathan"i | "lhurgoyf"i | "licid"i | "lizard"i | "manticore"i | "masticore"i
| ("mercenary"i|"mercenaries"i) | "merfolk"i | "metathran"i | "minion"i | "minotaur"i | "mole"i | "monger"i | "mongoose"i | "monk"i
| "monkey"i | "moonfolk"i | "mouse"i | "mutant"i | "myr"i | "mystic"i | "naga"i | "nautilus"i | "nephilim"i | "nightmare"i
| "nightstalker"i | "ninja"i | "noggle"i | "nomad"i | "nymph"i | ("octopus"i|"octopuses"i) | "ogre"i | "ooze"i | "orb"i | "orc"i
| "orgg"i | "ouphe"i | "ox"i | "oyster"i | "pangolin"i | "pegasus"i | "pentavite"i | "pest"i | "phelddagrif"i | "phoenix"i
| "pilot"i | "pincher"i | "pirate"i | "plant"i | "praetor"i | "prism"i | "processor"i | "rabbit"i | "rat"i | "rebel"i
| "reflection"i | "rhino"i | "rigger"i | "rogue"i | "sable"i | "salamander"i | "samurai"i | "sand"i | "saproling"i | "satyr"i
| "scarecrow"i | "scion"i | "scorpion"i | "scout"i | "serf"i | "serpent"i | "servo"i | "shade"i | "shaman"i | "shapeshifter"i
| "sheep"i | "siren"i | "skeleton"i | "slith"i | "sliver"i | "slug"i | "snake"i | "soldier"i | "soltari"i | "spawn"i | "specter"i
| "spellshaper"i | "sphinx"i | "spider"i | "spike"i | "spirit"i | "splinter"i | "sponge"i | "squid"i | "squirrel"i | "starfish"i
| "surrakar"i | "survivor"i | "tetravite"i | "thalakos"i | "thopter"i | "thrull"i | "treefolk"i | "trilobite"i | "triskelavite"i
| "troll"i | "turtle"i | "unicorn"i | "vampire"i | "vedalken"i | "viashino"i | "volver"i | "wall"i | "warrior"i | "warlock"i | "weird"i
| ("werewolf"i|"werewolves"i) | "whale"i | "wizard"i | ("wolf"i|"wolves"i) | "wolverine"i | "wombat"i | "worm"i | "wraith"i | "wurm"i | "yeti"i
| "zombie"i | "zubera"i
SUBTYPEPLANAR.10: "alara"i | "arkhos"i | "azgol"i | "belenon"i | "bolas’s meditation realm"
| "dominaria"i | "equilor"i | "ergamon"i | "fabacin"i | "innistrad"i | "iquatana"i | "ir"i
| "kaldheim"i | "kamigawa"i | "karsus"i | "kephalai"i | "kinshala"i | "kolbahan"i | "kyneth"i
| "lorwyn"i | "luvion"i | "mercadia"i | "mirrodin"i | "moag"i | "mongseng"i | "muraganda"i
| "new phyrexia" | "phyrexia"i | "pyrulea"i | "rabiah"i | "rath"i | "ravnica"i | "regatha"i
| "segovia"i | "serra’s realm" | "shadowmoor"i | "shandalar"i | "ulgrotha"i | "valla"i
| "vryn"i | "wildfire"i | "xerex"i | "zendikar"i
subtype.5: SUBTYPESPELL | SUBTYPELAND | SUBTYPEARTIFACT | SUBTYPEENCHANTMENT | SUBTYPEPLANESWALKER | SUBTYPECREATUREA | SUBTYPECREATUREB | SUBTYPEPLANAR
SUPERTYPE.5: "basic"i | "legendary"i | "ongoing"i | "snow"i | "world"i
DAMAGETYPE.10: "damage"i | "combat damage" | "excess damage"
ABILITYMODIFIER.5: "mana"i | "loyalty"i | "intrinsic"i //Example(s): "*mana* abilities", "*loyalty* abilities", "this permanent has the *intrinsic* mana ability '{T}: Add {W}.'"
RELATIVEMODIFIER.10: "next"i | "additional"i | "extra"i | "top"i | "bottom"i | "new"i //Example(s): "an *additional* +1/+1 counter", "take an *extra* turn"
playerstatusmodifier.5: negationofterm? "active"i | "defending"i //Example(s): "the *active* player", "the *defending* player"
KEYWORDSTATUSMODIFIER.5: "paired"i | "face-up" | "face-down" //TODO.5: This will need to be generalized more.
| "fortified"i | "monstrous"i
effectstatusmodifier.5: reservedverb_pastonly | reservedverb_participleorgerundonly
QUALIFIER.10: "card"i | "permanent"i | "source"i | "spell"i | "token"i | "effect"i
COLOR.5: "white"i | "blue"i | "black"i | "red"i | "green"i | "monocolored"i | "multicolored"i | "colorless"i
PHASE.10: BEGINNINGPHASE | MAINPHASE | COMBATPHASE | ENDINGPHASE | PHASEGENERIC
BEGINNINGPHASE.5: "beginning phrase"
MAINPHASE.5: ("precombat"i | "postcombat"i)? "main phase"
COMBATPHASE.5: "combat"i "phase"i?
ENDINGPHASE.5: "ending phase"
PHASEGENERIC.5: "phase"i["s"i]
STEP.10: UNTAPSTEP | UPKEEPSTEP | DRAWSTEP | BEGINNINGOFCOMBATSTEP
| DECLAREATTACKERSSTEP | DECLAREBLOCKERSSTEP | COMBATDAMAGESTEP | ENDSTEP | CLEANUPSTEP | STEPGENERIC
UNTAPSTEP.5: "untap step"
UPKEEPSTEP.5: "upkeep"i "step"i?
DRAWSTEP.5: "draw step"
BEGINNINGOFCOMBATSTEP.5: "beginning of combat" "step"i?
DECLAREATTACKERSSTEP.5: "declare attackers step"
COMBATDAMAGESTEP.5: "combat damage step"
DECLAREBLOCKERSSTEP.5: "declare blockers step"
ENDSTEP.5: "end step"
CLEANUPSTEP.5: "cleanup step"
STEPGENERIC.5: "step"i
TURN.5: "turn"i ["s"i]
GAME.5: "game"i ["s"i]


//For a list of all the counter types in Magic to date, see here: https://mtg.fandom.com/wiki/Counter_(marker)/Full_List.
countertype.5: "COUNTERTYPE"i | ARBITRARYWORD//TODO


//Rules for mana symbols
//TODO.5: Add in text mana descriptions here
manaterm.5: manasymbol
manasymbol.5: "{" manamarkerseq "}"
manamarkerseq.5: manamarker_color -> regularmanasymbol
| INT -> genericmanasymbol
| manamarker_halfmana manamarker_color -> halfmanasymbol
| manamarker_color "/" manamarker_phyrexian -> phyrexianmanasymbol
| manamarker_color "/" manamarker_color -> hybridmanasymbol
| INT "/" manamarker_color -> alternate2manasymbol
| manamarker_snow -> snowmanasymbol
| manamarker_colorless -> colorlessmanasymbol
| manamarker_x -> xmanasymbol
manamarker_halfmana.5: "H"i -> halfmarker
manamarker_color.5: "W"i -> whitemarker
| "U"i -> bluemarker
| "B"i -> blackmarker
| "R"i -> redmarker
| "G"i -> greenmarker
manamarker_snow.5: "S"i -> snowmarker
manamarker_phyrexian.5: "P"i -> phyrexianmarker
manamarker_colorless.5: "C"i -> colorlessmarker
manamarker_x.5: "X"i -> xmarker

//Rules for the tap and untap symbols
tapuntapsymbol.5: TAPSYMBOL | UNTAPSYMBOL
TAPSYMBOL.5: "{T}"i
UNTAPSYMBOL.5: "{Q}"i

//Rules for representing numeric values
valueterm.5: INT | SIGNED_INT  | CARDINAL ("time"i["s"i])?| ORDINAL | FREQUENCY | variablevalue | manaterm
FREQUENCY.5: "once"i | "twice"i
CARDINAL.10: "one"i | "two"i | "three"i | "four"i | "five"i | "six"i | "seven"i | "eight"i | "nine"i | "ten"i
| "eleven"i | "twelve"i | "thirteen"i | "fourteen"i | "fifteen"i | "sixteen"i | "seventeen"i | "eighteen"i | "nineteen"i | "twenty"i //TODO: Extend this list.
ORDINAL.5: "first"i | "second"i | "third"i | "fourth"i | "fifth"i | "sixth"i | "seventh"i | "eighth"i | "ninth"i | "tenth"i //TODO: Extend this list.
variablevalue.5: VARIABLEVALUESYMBOL | definednumberterm
definednumberterm.5: NUMBERPROPERTY? "number"i["s"i] //Example(s): "the *number* of cards you drew this term", "a *prime number* of lands"
| ("much"i | "many"i) //Example: "create *that many* treasure tokens", "draw twice *that many* cards"
VARIABLEVALUESYMBOL.5: "x"i | "*"
NUMBERPROPERTY.5: "odd"i | "even"i | "prime"i
