//Expressions make things happen in the game. When spell states "~ *deals* 3 damage to target creature", "nonbasic lands *are* mountains",
//or "target player *loses* the game", damage is dealt, lands become mountains, and a player loses the game. Expressions take declarations
//or other expressions (like who deals the damage and to whom is the damage dealt). Meanwhile, statements can contain expressions, like the
//if statement "if you control a modified creature, draw a card" is equivalent to (if the expression "you control a modified creature" evaluates
//to true, make the expression "draw a card" happen.
expression: beingexpression | keywordactionexpression
!expressionordeclaration: expression | declaration

beingexpression: expressionordeclaration ("is" | "was" | "are" "each"?) ("still"|"not")? expressionordeclaration -> isexpression
| expressionordeclaration ("has"|"have"|"had") expressionordeclaration -> hasexpression
| expressionordeclaration "can" "not"? expressionordeclaration -> canexpression
| expressionordeclaration "become"["s"] expressionordeclaration -> becomesexpression
| expressionordeclaration "may" expressionordeclaration -> mayexpression
| expressionordeclaration "would" expressionordeclaration -> wouldexpression
| expressionordeclaration (expressionordeclaration? ("do"["es"]? | "did") "not"? expressionordeclaration? | "doing" "so" ) -> doesexpression
| "be" expressionordeclaration -> beexpression //Example(s): "*be* attacked", "*be* sacrificed"

keywordactionexpression: keywordactionsubject? keywordactionverb keywordactionobjectsandmodifiers*
keywordactionsubject: expressionordeclaration
keywordactionverb: keywordactionadverb? KEYWORDACTION
keywordactionadverb: "secretly"
keywordactionobjectsandmodifiers: KEYWORDACTIONPREPOSITION? expressionordeclaration | "this way"
KEYWORDACTIONPREPOSITION.10: "on" | "from" | "to" | "with"["out"] | "as" "though"? | "into" //TODO: Do we handle "as (though)" at the statement level?

//"on" "from" "to" "without"
//"as though" "as an additional cost to"
//"able" (see ableexpression)
//"a spell *targets* a player"

KEYWORDACTION.10: "activat"["es" | "ed"]
| "attack"["s" | "ed" | "ing"]
| "block"["s" | "ed" | "ing"]
| "attach"["es" | "ed" | "ing"]
| "cast"["s" | "ing"]
| ("choos"["e"]["s" | "ing"] | "chose")
| "control"["s" | "led" | "ling" ]
| "gain"["s" | "ed" | "ing"]
| ("los"["e"]["s" | "d" | "ing"] | "lost")
| "counter"["s" | "ed" | "ing"]
| "creat"["e"]["s" | "d" | "ing"]
| "destroy"["s" | "ed" | "ing"]
| "draw"["s" | "n" | "ing"]
| "discard"["s" | "ed" | "ing"]
| "doubl"["e"]["s" | "d" | "ing"]
| "exchang"["e"]["s" | "d" | "ing"]
| "exil"["e"]["s" | "d" | "ing"]
| ("fight"["s" | "ing"] | "fought")
| "play"["s" | "ed" | "ing"]
| "reveal"["s" | "ed" | "ing"]
| "sacrific"["e"]["s" | "d" | "ing"]
| "search"["es" | "ed" | "ing"]
| "shuffl"["es" | "ed" | "ing"]
| "un"? "tap"["s" | "ped" | "ping"]
| "milL"["s" | "ed" | "ing"]
| "declar"["e"]["s" | "d" | "ing"]
| "deal"["s" | "t" | "ing"]
| "prevent"["s" | "ed" | "ing"]
| "return"["s" | "ed" | "ing" ]
| "put"["s" | "ing"]
| "remov"["es" | "ed" | "ing"]
| ("spend"["s" | "ing"] | "spent")
| ("pay"["s" | "ing"] | "paid")
| ("get"["s" | "ing"] | "got")
| ("tak"["es" | "ing"] | "took")
| ("die"["s" | "d"] | "dying")
| "add"["s" | "ed" | "ing"]
| "look"["s" | "ed" | "ing"]
| "flip"["s" | "ped" | "ping"]
| ("win"["s" | "ning"] | "won")
| "remain"["s" | "ing"]
| "look"["s" | "ed" | "ing"]
| "assign"["s" | "ed" | "ing"]
| "chang"["e"]["s" | "d" | "ing"]
| "skip"["s" | "ped" | "ing"]
| "switch"["es" | "ed" | "ing"]
| "shar"["es" | "ed" | "ing"] //TODO: conflict with "shard"?
| "regenerat"["e"]["s" | "d" | "ing"]
| "scry"["s" | "ed" | "ing"]
| "fateseal"
| "clash"["es" | "ed" | "ing"]
| "detain"["s" | "ed" | "ing"]
| "planeswalk"["s" | "ed" | "ing"]
| "set in motion"
| "abandon"["s" | "ed" | "ing"]
| "proliferat"["e"]["s" | "d" | "ing"]
| "transform"["s" | "ed" | "ing"]
| "populate"["s" | "d" | "ing"]
| "vote"["s" | "d" | "ing"]
| "bolster"["s" | "ed" | "ing"]
| "manifest"["s" | "ed" | "ing"] "dread"?
| "support"["s" | "ed" | "ing"]
| "investigat"["e"]["s" | "d" | "ing"]
| "meld"
| "goad"["s" | "ed" | "ing"]
| "exert"["s" | "ed" | "ing"]
| "explor"["e"]["s" | "d" | "ing"]
| "turn"["s" | "ed" | "ing"] //TODO: conflict with turn as in time
| "cycl"["e"]["s" | "d" | "ing"] //TODO: conflict with, say, "wizardcycling"
| "surveil"["s" | "ed" | "ing"]
| "conniv"["e"]["s" | "d" | "ing"]
| "adapt"["s" | "ed" | "ing"]
| "amass"["es" | "ed" | "ing"]
| "learn"["s" | "ed" | "ing"]
| "incubat"["e"]["s" | "d" | "ing"]
| "tempt"["s" | "ed" | "ing"]
| "time travel"["s" | "ed" | "ing"]
| "discover"["s"]
| "time travel"["s" | "ed" | "ing"]
| "cloak"["s" | "ed" | "ing"]
| "collect"["s" | "ed" | "ing"] //As in collecting evidence
| "suspect"["s" | "ed" | "ing"]
| "forage"["e"]["s" | "d" | "ing"]
| "gift"["s" | "ed" | "ing"]
| "enter"["s" | "ed" | "ing"]
| ("leav"["e"]["s" | "ing"] | "left")
| "caus"["e"]["s" | "d" | "ing"]